plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.sgs.citytax"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"
        // Add config properties as build configs
        rootProject.ext.config.each { p ->
            buildConfigField 'String', p.key, p.value
            resValue 'string', p.key, p.value
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {

            versionNameSuffix "-DEBUG"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            versionNameSuffix "-RELEASE"
        }
    }

    signingConfigs {
        debug {
            storePassword KEYSTORE_PASSWORD
            keyAlias = KEYSTORE_ALIAS
            keyPassword KEYSTORE_PASSWORD
            storeFile file(DEBUG_KEYSTORE_PATH)
        }
        release {
            storePassword KEYSTORE_PASSWORD
            keyAlias = KEYSTORE_ALIAS
            keyPassword KEYSTORE_PASSWORD
            storeFile file(RELEASE_KEYSTORE_PATH)
        }
    }

    flavorDimensions 'default'
    productFlavors {
        dev {
            dimension = 'default'
            applicationId "com.sgs.citytax.dev"
            versionCode 140
            versionName "1.0 DEV"
            signingConfig signingConfigs.debug
            resValue 'string', 'images_file_path', "Android/data/" + applicationId + "/files/Pictures"
        }
        qa {
            dimension = 'default'
            applicationId "com.sgs.citytax.qa"
            versionCode 384
            versionName "1.0 QA"
            signingConfig signingConfigs.debug
            resValue 'string', 'images_file_path', "Android/data/" + applicationId + "/files/Pictures"
        }
        uat {
            dimension = 'default'
            applicationId "com.sgs.citytax.uat"
            versionCode 119
            versionName "1.0 UAT"
            signingConfig signingConfigs.debug
            resValue 'string', 'images_file_path', "Android/data/" + applicationId + "/files/Pictures"
        }
        prod {
            dimension = 'default'
            applicationId "com.sgs.citytax"
            versionCode 18
            versionName "1.0"
            signingConfig signingConfigs.release
            resValue 'string', 'images_file_path', "Android/data/" + applicationId + "/files/Pictures"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            file('src/main/assets')
                    .listFiles()
                    .each { res.srcDirs += it.path }
        }
    }

    buildFeatures{
        viewBinding true
        dataBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.preference:preference:1.1.1'

    //region Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.1.0'
    //endregion

    //region Zxing for Barcode Scanner
    implementation 'me.dm7.barcodescanner:zxing:1.9.13'
    //endregion

    //region For Orange wallet xml converter
    implementation('com.squareup.retrofit2:converter-simplexml:2.1.0') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }
    //endregion

    //region SUNMI
    implementation 'com.sunmi:sunmiui:1.1.27'
    implementation 'com.sunmi:printerlibrary:1.0.6'
    //endregion

    //region Glide
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    //endregion

    //region Location services
    implementation "com.google.android.gms:play-services-location:17.0.0"
    //endregion

    //region Map Service
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    //endregion

    //region Work Manager
    implementation "androidx.work:work-runtime-ktx:2.3.4"
    //endregion

    //region map util area
    implementation 'com.google.maps.android:android-maps-utils:0.6.2'
    //endregion

    //region Signature
    implementation 'com.github.gcacace:signature-pad:1.2.1'
    //endregion

    //region TreeView structure
    implementation 'com.github.johnkil.print:print:1.2.2'
    //endregion

    //region PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    //endregion

    //region Jedis
    implementation 'redis.clients:jedis:3.7.0'
    //endregion

    //region Google API Client
    implementation 'com.google.api-client:google-api-client-android:1.20.0'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    //endregion

    //region testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    //endregion
}